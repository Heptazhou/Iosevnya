$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-OU : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [UpperOuShape top] : glyph-proc
		include : OShape (top * HBarPos + HalfStroke) 0 SB RightSB nothing SmoothA SmoothB
		include : dispiro
			widths.lhs
			flat SB top [heading Downward]
			curl SB (top * HBarPos - O - HalfStroke + SmoothB)
			arcvh
			g4 (Middle + CorrectionOMidS) (top * HBarPos - O - HalfStroke + (Stroke * (1 - CThin))) [widths.lhs (Stroke * CThin)]
			archv
			flat RightSB (top * HBarPos - O - HalfStroke + SmoothA) [widths.lhs]
			curl RightSB top [heading Upward]

	create-glyph 'OU' 0x222 : glyph-proc
		include : MarkSet.capital
		include : UpperOuShape CAP

	create-glyph 'smcpOU' 0x1D15 : glyph-proc
		include : MarkSet.e
		include : UpperOuShape XH

	create-glyph 'ou' 0x223 : glyph-proc
		include : MarkSet.b
		local bar : StrokeWidthBlend (XH * 0.9) XH
		include : OShape bar 0 SB RightSB
		include : dispiro
			widths.lhs
			flat SB CAP [heading Downward]
			curl SB (bar - O - Stroke + SmoothB)
			arcvh
			g4 (Middle + CorrectionOMidS) (bar - O - Stroke + (Stroke * (1 - CThin))) [widths.lhs (Stroke * CThin)]
			archv
			flat RightSB (bar - O - Stroke + SmoothA) [widths.lhs]
			curl RightSB CAP [heading Upward]
