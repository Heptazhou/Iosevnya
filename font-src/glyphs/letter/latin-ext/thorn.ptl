$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Latin-Thorn : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : LeaningAnchor
	glyph-block-import Letter-Shared-Shapes : OBarLeft LetterBarOverlay SerifFrame
	glyph-block-import Letter-Latin-Lower-P

	define xThornLeftStroke : SB * 1.25
	define yThornBowlBot : CAP * 0.25 + [if SLAB (Stroke * 0.125) 0]
	define yThornBowlTop : CAP - 0.7 * yThornBowlBot + [if SLAB (-0.125) 0.25] * Stroke
	define [ThornShape yBowlBot yBowlTop] : glyph-proc

		local turn : mix yBowlTop yBowlBot (ArchDepthB / (ArchDepthA + ArchDepthB))
		local turnRadius : (yBowlTop - yBowlBot) / 2

		include : dispiro
			widths.rhs
			flat xThornLeftStroke yBowlTop [heading Rightward]
			curl (RightSB - turnRadius - CorrectionOMidS) yBowlTop
			archv
			g4   (RightSB - O) turn
			arcvh
			flat (RightSB - turnRadius + CorrectionOMidS) yBowlBot
			curl xThornLeftStroke yBowlBot [heading Leftward]

		include : VBar.l xThornLeftStroke 0 CAP
		if SLAB : begin
			local sf : SerifFrame Ascender 0 xThornLeftStroke RightSB
			include : composite-proc sf.lt.fullSide sf.lb.fullSide

	create-glyph 'Thorn' 0xDE : glyph-proc
		include : MarkSet.capital
		include : ThornShape yThornBowlBot yThornBowlTop
		include : LeaningAnchor.Above.VBar.l xThornLeftStroke
		include : LeaningAnchor.Below.VBar.l xThornLeftStroke

	create-glyph 'grek/Sho' 0x3F7 : glyph-proc
		include : MarkSet.capital
		local d : mix yThornBowlBot (CAP - yThornBowlTop) 0.5
		include : ThornShape d (CAP - d)
		include : LeaningAnchor.Above.VBar.l xThornLeftStroke
		include : LeaningAnchor.Below.VBar.l xThornLeftStroke

	create-glyph 'ThornBarTop/Overlay' : LetterBarOverlay.l.in
		x   -- xThornLeftStroke
		top -- (CAP - [if SLAB Stroke 0])
		bot -- (CAP - [mix yThornBowlBot (CAP - yThornBowlTop) 0.5])
	create-glyph 'thornBarTop/Overlay' : LetterBarOverlay.l.in
		x   -- SB
		top -- (Ascender - [if SLAB Stroke 0])
		bot -- XH
	create-glyph 'ThornBarBot/Overlay' : LetterBarOverlay.l.in
		x   -- xThornLeftStroke
		bot -- (0 + [if SLAB Stroke 0])
		top -- [mix yThornBowlBot (CAP - yThornBowlTop) 0.5]
	create-glyph 'thornBarBot/Overlay' : LetterBarOverlay.l.in
		x   -- SB
		bot -- (Descender + [if SLAB Stroke 0])
		top -- 0

	derive-composites 'ThornBarTop' 0xA764 'grek/Sho' 'ThornBarTop/Overlay'
	derive-composites 'thornBarTop' 0xA765 'thorn' 'thornBarTop/Overlay'
	derive-composites 'ThornBarBot' 0xA766 'grek/Sho' 'ThornBarBot/Overlay'
	derive-composites 'thornBarBot' 0xA767 'thorn' 'thornBarBot/Overlay'
