$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"
import [DependentSelector] from"../../../support/gr.mjs"

glyph-module

glyph-block Letter-Latin-Lower-E : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateSelectorVariants DefineSelectorGlyph
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth RetroflexHook
	glyph-block-import Letter-Shared-Shapes : SerifedArcEnd ArcEndSerif DToothlessRise
	glyph-block-import Mark-Shared-Metrics : markExtend markStroke markStress markFine
	glyph-block-import Mark-Above : aboveMarkTop aboveMarkBot aboveMarkMid aboveMarkStack
	glyph-block-import Mark-Adjustment : ExtendBelowBaseAnchors
	glyph-block-import Letter-Latin-C : CConfig

	define [xTerminalR df] : df.rightSB - OX * [if para.isItalic 0 0.5]

	define [HookHeight top stroke] : Math.min Hook (AHook / XH * top)
		if para.isItalic top (stroke / 2 + (top - stroke * 3) / 4)

	define SLAB-NONE       0
	define SLAB-CLASSICAL  1
	define SLAB-INWARD     2

	define [SmallESerifedTerminalShape df top stroke tailSlab] : match tailSlab
		[Just SLAB-CLASSICAL] : begin
			SerifedArcEnd.LtrLhs df.rightSB df.middle 0 stroke [HookHeight top stroke]
		[Just SLAB-INWARD] : list
			arcvh
			g4 (df.middle + CorrectionOMidX * stroke) O
			g4 df.rightSB (DToothlessRise)
		__ : list
			hookend O (sw -- stroke)
			g4 [xTerminalR df] [HookHeight top stroke]

	define [SmallETerminalSerif df top stroke tailSlab] : match tailSlab
		[Just SLAB-CLASSICAL] : ArcEndSerif.R df.rightSB 0 stroke [HookHeight top stroke]
		[Just SLAB-INWARD] : ArcEndSerif.InwardR df.rightSB 0 stroke [HookHeight top stroke]
		__ : no-shape

	glyph-block-export SmallEShape
	define [SmallEShape] : with-params [df top stroke barpos [bbd 0] tailSlab] : glyph-proc
		local barbottom : top * [fallback barpos DesignParameters.eBarPos] - (stroke / 2)

		include : HBar.b (df.leftSB + (stroke / 2) + OX + bbd) (df.rightSB - (stroke / 2) - OX) barbottom stroke
		local path : include : dispiro
			widths.lhs stroke
			flat (df.rightSB - OX) barbottom [heading Upward]
			curl (df.rightSB - OX) (top - [df.archDepthB SmallArchDepth])
			arcvh
			g4 (df.middle - CorrectionOMidS) (top - O)
			archv
			flat (df.leftSB + OX) (top - [df.archDepthA SmallArchDepth])
			curl (df.leftSB + OX) (0 + [df.archDepthB SmallArchDepth])
			SmallESerifedTerminalShape df top stroke tailSlab

		include : SmallETerminalSerif df top stroke tailSlab

		return path.rhsKnots.[path.rhsKnots.length - 1]

	glyph-block-export RevSmallEShape
	define [RevSmallEShape] : with-params [df top stroke barpos] : glyph-proc
		local barbottom (top * [fallback barpos DesignParameters.eBarPos] - HalfStroke)
		local hookx df.leftSB
		local hookmiddle : [mix (df.rightSB - O) hookx 0.55] + CorrectionOMidS

		include : HBar.b (df.leftSB + (stroke / 2)) (df.rightSB - (stroke / 2)) barbottom stroke
		include : dispiro
			widths.rhs stroke
			flat (df.leftSB + OX) barbottom [heading Upward]
			curl (df.leftSB + OX) (top - [df.archDepthA SmallArchDepth])
			arcvh
			g4   df.middle (top - O)
			archv
			flat (df.rightSB - OX) (top - [df.archDepthB SmallArchDepth])
			curl (df.rightSB - OX) [df.archDepthA SmallArchDepth]
			hookend O (sw -- stroke)
			g4 (df.width - [xTerminalR df]) [HookHeight top stroke]

	glyph-block-export SmallERoundedShape
	define [SmallERoundedShape] : with-params [df top stroke barpos tailSlab] : glyph-proc
		local barbottom : top * [fallback barpos : if para.isItalic 0.500 0.475] - (stroke / 2)

		local pBarRight : 0.475 - TanSlope * 0.5
		local pArcRight : if para.isItalic (0.425 - TanSlope * 0.25) (ArchDepthA / (ArchDepthA + ArchDepthB))

		local xStart : df.leftSB + [HSwToV : 0.125 * stroke]
		local pfIt : if para.isItalic 1 0
		local path : include : dispiro
			widths.lhs stroke
			[if para.isItalic g2 flat] xStart (barbottom - pfIt * [StrokeWidthBlend 2 3] * O)
			if para.isItalic [alsoThru.g2 0.5 0.8] [list]
			[if para.isItalic g2 curl] [mix (xStart + [if para.isItalic 0.25 0.0] * [HSwToV stroke]) df.rightSB pBarRight] (barbottom + pfIt * [StrokeWidthBlend 0.25 1] * O)
			if para.isItalic {} [archv]
			g4 (df.rightSB - OX) [mix barbottom top pArcRight]
			arcvh
			g4 (df.middle - CorrectionOMidS) (top - O)
			archv
			flat (df.leftSB + OX) (top - [df.archDepthA SmallArchDepth])
			curl (df.leftSB + OX) (0 + [df.archDepthB SmallArchDepth])
			SmallESerifedTerminalShape df top stroke tailSlab

		include : SmallETerminalSerif df top stroke tailSlab

		return path.rhsKnots.[path.rhsKnots.length - 1]

	glyph-block-export RevSmallERoundedShape
	define [RevSmallERoundedShape] : with-params [df top stroke barpos] : glyph-proc
		local barbottom : top * [fallback barpos : if para.isItalic 0.500 0.475] - (stroke / 2)

		local pBarRight : 0.475 - TanSlope * 0.5
		local pArcRight : if para.isItalic (0.425 + TanSlope * 0.25) (ArchDepthB / (ArchDepthA + ArchDepthB))

		local xStart : df.rightSB - [HSwToV : 0.125 * stroke]
		local pfIt : if para.isItalic 1 0
		include : dispiro
			widths.rhs stroke
			[if para.isItalic g2 flat] xStart (barbottom - pfIt * [StrokeWidthBlend 2 3] * O)
			if para.isItalic [alsoThru.g2 0.5 0.8] [list]
			[if para.isItalic g2 curl] [mix (xStart - [if para.isItalic 0.25 0.0] * [HSwToV stroke]) df.leftSB pBarRight] (barbottom + pfIt * [StrokeWidthBlend 0.25 1] * O)
			if para.isItalic {} [archv]
			g4 (df.leftSB + OX) [mix barbottom top pArcRight]
			arcvh
			g4 (df.middle - CorrectionOMidS) (top - O)
			archv
			flat (df.rightSB - OX) (top - [df.archDepthB SmallArchDepth])
			curl (df.rightSB - OX) (0 + [df.archDepthA SmallArchDepth])
			hookend O (sw -- stroke)
			g4 (df.width - [xTerminalR df]) [HookHeight top stroke]

	define [AbkCheShape] : with-params [fDesc Body df top tailSlab] : glyph-proc
		local gap : (df.width - 2 * df.leftSB - 2.5 * df.mvs) * 0.375 - [HSwToV : 0.25 * df.mvs]
		define divSub : (df.width - gap - df.mvs) / Width
		define dfSub : DivFrame divSub 2
		include : Body dfSub top df.mvs (tailSlab -- tailSlab)
		if fDesc : begin
			include : ExtendBelowBaseAnchors (-LongJut + 0.5 * Stroke)
			include : difference
				VBar.m dfSub.middle (-LongJut + 0.5 * Stroke) (df.mvs + O) [AdviceStroke 3.5 df.div]
				OShapeOutline.NoOvershoot top 0 dfSub.leftSB dfSub.rightSB df.mvs
		include : Translate (Width * (df.div - divSub)) 0

		local hd : FlatHookDepth df
		local yBar : top * DesignParameters.eBarPos - 0.5 * df.mvs
		include : intersection [MaskLeft (dfSub.leftSB + Width * (df.div - divSub))] : dispiro
			flat (df.leftSB - [HSwToV : 0.25 * df.mvs]) (yBar + Hook) [widths.lhs.heading df.mvs Downward]
			curl (df.leftSB - [HSwToV : 0.25 * df.mvs]) (yBar + [Math.min Hook hd.y] - df.mvs * 0.25) [heading Downward]
			arcvh
			flat [Math.min (df.leftSB + hd.x - [HSwToV : 0.5 * df.mvs]) (dfSub.leftSB + Width * (df.div - divSub))] yBar
			curl (dfSub.middle + Width * (df.div - divSub)) yBar

	define SmallEConfig : object
		flatCrossbar { SmallEShape        RevSmallEShape        }
		rounded      { SmallERoundedShape RevSmallERoundedShape }

	foreach { suffix { Body RevBody } } [Object.entries SmallEConfig] : do
		create-glyph "e.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : Body [DivFrame 1] XH [AdviceStroke2 2 3 XH]

		create-glyph "eOgonek.\(suffix)" : glyph-proc
			include : MarkSet.e
			local lastKnot : include : Body [DivFrame 1] XH [AdviceStroke2 2 3 XH]
			include : refer-glyph 'ogonekTR/spacer'

			# Connected Ogonek shape
			local fine : AdviceStroke 8
			local depth : 0 - Descender - markStroke
			local extL : (7 / 16) * depth + 0.25 * markStress
			local extR : Math.max (0.0625 * markExtend) (1.5 * TanSlope * markStroke)
			local beginCoSlope : if para.isItalic 0.2 0

			set-base-anchor 'trailing' (RightSB + extR) (-depth + 0.5 * O - markStroke)
			set-base-anchor 'belowBraceL' (RightSB - extL - [HSwToV : 0.25 * markStroke]) (-0.75 * depth)
			set-base-anchor 'belowBraceR' (RightSB - 0.75 * extL) (-0.75 * depth)

			local turnSlope : 0.5 * ((markStroke - fine) / markStroke - (ArchDepthB - ArchDepth) / ArchDepth)

			include : intersection
				MaskBelow lastKnot.y
				dispiro
					g4 lastKnot.x lastKnot.y [widths.rhs fine]
					g4 (lastKnot.x - beginCoSlope * 0.01) (lastKnot.y - 0.01)
					alsoThruThem.fromTWithOffset {(1/3) (2/3)} : object
						rx       : function [rt] rt
						deltaX   : function [rt] 0
						ry       : function [rt] : 1/24 + rt + (1/2 - rt) * (3/8)
						deltaY   : function [rt] (-0.25 * [mix fine markStroke rt])
						modifier : function [rt] : widths.rhs [mix fine markStroke : Math.pow rt 2]
					g4.down.mid (RightSB - extL) (-0.75 * depth) [widths.rhs.heading markStroke {.x HVContrast .y turnSlope}]
					arcvh [widths.rhs markStroke]
					g4 (RightSB + [mix (-extL) extR (11/16)]) (-depth + O) [heading Rightward]
					g4 (RightSB + extR) (-depth + 0.5 * O) [heading Rightward]

		create-glyph "eWithNotch.\(suffix)" : glyph-proc
			include : MarkSet.e
			local lastKnot : include : Body [DivFrame 1] XH [AdviceStroke2 2 3 XH]
			local sw : AdviceStroke 4
			local ry : Math.min (lastKnot.y - sw) (XH * 0.08)
			local rx : Math.min ry
			include : dispiro
				g4.down.start lastKnot.x lastKnot.y [widths.rhs sw]
				arcvh
				g4 (lastKnot.x + rx) (lastKnot.y - ry + O)
				archv
				g4.up.end (lastKnot.x + rx * 2) lastKnot.y

		create-glyph "eRetroflexHook.\(suffix)" : glyph-proc
			include : MarkSet.e
			local lastKnot : include : Body [DivFrame 1] XH [AdviceStroke2 2 3 XH]
			include : RetroflexHook.r
				x -- lastKnot.x
				y -- 0
				yAttach -- lastKnot.y

		create-glyph "eRev.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : RevBody [DivFrame 1] XH [AdviceStroke2 2 3 XH]

		create-glyph "eBar.\(suffix)" : glyph-proc
			include [refer-glyph "e.\(suffix)"] AS_BASE ALSO_METRICS
			include : HBar.m [mix SB 0 0.7] [mix RightSB Width 0.7] (XH * 0.25 + Stroke * 0.25)
				Math.min [AdviceStroke 5] (0.25 * (XH - 3 * Stroke))

		DefineSelectorGlyph "Schwa" suffix [MarkSet.capital]
		DefineSelectorGlyph "schwa" suffix [MarkSet.e]

		define abkCheDf : DivFrame para.diversityM 3

		DefineSelectorGlyph "cyrl/abk/Che" suffix [abkCheDf.markSet.capital]
		DefineSelectorGlyph "cyrl/abk/che" suffix [abkCheDf.markSet.e]
		DefineSelectorGlyph "cyrl/abk/CheDescender" suffix [abkCheDf.markSet.capDesc]
		DefineSelectorGlyph "cyrl/abk/cheDescender" suffix [abkCheDf.markSet.p]

		foreach { suffixSerif { styTop styBot } } [Object.entries CConfig] : do
			create-glyph "Schwa.\(suffix).\(suffixSerif)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : Body [DivFrame 1] CAP [AdviceStroke2 2 3 CAP] (tailSlab -- styTop)
				include : FlipAround Middle (CAP / 2)
			create-glyph "schwa.\(suffix).\(suffixSerif)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : Body [DivFrame 1] XH [AdviceStroke2 2 3 XH] (tailSlab -- styTop)
				include : FlipAround Middle (XH / 2)

			create-glyph "cyrl/abk/Che.\(suffix).\(suffixSerif)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : AbkCheShape 0 Body abkCheDf CAP (tailSlab -- styBot)
			create-glyph "cyrl/abk/che.\(suffix).\(suffixSerif)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : AbkCheShape 0 Body abkCheDf XH (tailSlab -- styBot)
			create-glyph "cyrl/abk/CheDescender.\(suffix).\(suffixSerif)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : AbkCheShape 1 Body abkCheDf CAP (tailSlab -- styBot)
			create-glyph "cyrl/abk/cheDescender.\(suffix).\(suffixSerif)" : glyph-proc
				set-width 0
				set-mark-anchor 'cvDecompose' 0 0
				include : AbkCheShape 1 Body abkCheDf XH (tailSlab -- styBot)

		select-variant "Schwa.\(suffix)" (follow -- "CTopSerifOnly")
		select-variant "schwa.\(suffix)" (follow -- "cTopSerifOnly")
		select-variant "cyrl/abk/Che.\(suffix)" (follow -- 'CBottomSerifOnly')
		select-variant "cyrl/abk/che.\(suffix)" (follow -- 'cBottomSerifOnly')
		select-variant "cyrl/abk/CheDescender.\(suffix)" (follow -- 'CBottomSerifOnly')
		select-variant "cyrl/abk/cheDescender.\(suffix)" (follow -- 'cBottomSerifOnly')

	select-variant 'e' 'e'
	alias 'cyrl/ie' 0x435 'e'
	turned 'turne' 0x1DD 'e' Middle (XH / 2)

	select-variant 'eOgonek' 0x119 (follow -- 'e')
	select-variant 'eRetroflexHook' 0x1D92 (follow -- 'e')
	select-variant 'eWithNotch' 0x2C78 (follow -- 'e')

	select-variant 'eRev' 0x258 (follow -- 'e')

	select-variant 'eBar' 0xAB33 (follow -- 'e')

	CreateSelectorVariants "Schwa" 0x18F [Object.keys SmallEConfig]
	alias 'cyrl/Schwa' 0x4D8 'Schwa'

	CreateSelectorVariants "schwa" 0x259 [Object.keys SmallEConfig]
	alias 'cyrl/schwa' 0x4D9 'schwa'

	CreateSelectorVariants 'cyrl/abk/Che' 0x4BC [Object.keys SmallEConfig] (follow -- 'e')
	CreateSelectorVariants 'cyrl/abk/che' 0x4BD [Object.keys SmallEConfig] (follow -- 'e')
	CreateSelectorVariants 'cyrl/abk/CheDescender' 0x4BE [Object.keys SmallEConfig] (follow -- 'e')
	CreateSelectorVariants 'cyrl/abk/cheDescender' 0x4BF [Object.keys SmallEConfig] (follow -- 'e')

	glyph-block-import Letter-Blackboard : BBS BBD
	create-glyph 'mathbb/e' 0x1D556 : glyph-proc
		include : MarkSet.e
		include : SmallEShape [DivFrame 1] XH BBS (bbd -- BBD)
		include : intersection
			OShapeOutline.NoOvershoot XH 0 SB RightSB BBS
			union
				VBar.l (SB + BBD + OX) 0 XH BBS
				VBar.r (RightSB - BBD - OX) (XH * DesignParameters.eBarPos) XH BBS

