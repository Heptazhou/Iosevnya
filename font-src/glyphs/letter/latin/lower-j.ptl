$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose MathSansSerif] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Lower-J : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateDottedComposition
	glyph-block-import Letter-Shared-Shapes : CurlyTail FlatHookDepth
	glyph-block-import Letter-Shared-Shapes : DiagonalTailL DiagonalTailStdDepth
	glyph-block-import Letter-Blackboard : BBS BBD

	### j and dotless j
	create-glyph 'dotlessj.serifless' : glyph-proc
		include : MarkSet.p
		local center : Middle + JBalance + HalfStroke * HVContrast
		set-base-anchor 'above' (center - HVContrast * HalfStroke) XH
		set-base-anchor 'overlay' (center - HVContrast * HalfStroke) (XH / 2)
		local hookx  : center - (Width * 0.5) - Stroke * HVContrast + OXHook
		local turn : [mix center hookx 0.5] + CorrectionOMidS
		include : dispiro
			widths.rhs
			flat center XH [heading Downward]
			curl center (Descender + SmoothA)
			hookend (Descender + O)
			g4 hookx (Descender + JHook)

	create-glyph 'mathbb/dotlessj' : glyph-proc
		include : MarkSet.p
		local center : Middle + JBalance + BBD / 2
		set-base-anchor 'above'   (center - HVContrast * BBS / 2 - BBD / 2) XH
		set-base-anchor 'overlay' (center - HVContrast * BBS / 2 - BBD / 2) (XH / 2)
		local hookx  : center - (Width * 0.5) - BBD * HVContrast + OXHook
		local turn : [mix center hookx 0.5] + CorrectionOMidS
		include : dispiro
			widths.rhs BBS
			flat center XH [heading Downward]
			curl center (Descender + SmoothA)
			hookend (Descender + O)
			g4 hookx (Descender + JHook)
		include : HBarTop (center - BBD) center XH BBS
		include : intersection
			VBarRight (center - BBD) Descender XH BBS
			spiro-outline
				corner center XH [heading Downward]
				curl center (Descender + SmoothA)
				hookend (Descender + O)
				g4 hookx (Descender + JHook)
				corner hookx XH

	define [OverrideJMarks df balance] : glyph-proc
		set-base-anchor 'above' (df.middle + balance - Stroke * 0.166 * HVContrast) XH
		set-base-anchor 'overlay' (df.middle + balance - Stroke * 0.166 * HVContrast) (XH / 2)

	create-glyph 'mathbb/j' 0x1D55B : glyph-proc
		include [refer-glyph 'mathbb/dotlessj'] AS_BASE ALSO_METRICS
		include [refer-glyph 'mathbb/dotAbove']
		Dotless.set currentGlyph 'mathbb/dotlessj'

	create-glyph 'dotlessj.straightLine' : glyph-proc
		local df : DivFrame para.diversityII
		set-width df.width
		include : df.markSet.p
		include : VBar df.middle Descender XH

	create-glyph 'dotlessj.hooky' : glyph-proc
		local df : DivFrame para.diversityII
		set-width df.width
		include : df.markSet.p
		local balance : IBalance2 df
		include : OverrideJMarks df balance
		include : LeftwardTopSerif (df.middle + balance) XH (LongJut * df.div)
		include : VBar (df.middle + balance) Descender XH

	create-glyph 'dotlessj.serifed' : glyph-proc
		include : MarkSet.p
		include [refer-glyph 'dotlessj.serifless'] AS_BASE
		include : OverrideJMarks [DivFrame 1] JBalance
		include : LeftwardTopSerif (Middle + JBalance) XH LongJut

	glyph-block-export FlatHookDotlessJShape
	define [FlatHookDotlessJShape] : params [df dfHook top crossLeft barCenter serif] : glyph-proc
		local terminal : crossLeft - OX * 2
		local bar : barCenter + HalfStroke * HVContrast
		local hd : FlatHookDepth dfHook
		if serif : include : LeftwardTopSerif barCenter top (LongJut * df.div)
		include : dispiro
			widths.lhs
			flat [Math.min terminal (bar - hd.x - [Math.max (Stroke / 3) (df.width / 6)])] Descender
			curl (bar - hd.x) Descender
			archv.superness DesignParameters.tightHookSuperness
			flat bar (Descender + hd.y)
			curl bar top [heading Upward]
		set-base-anchor 'above' barCenter top
		set-base-anchor 'overlay' barCenter (top / 2)

	create-glyph 'dotlessj.flatHookSerifless' : glyph-proc
		local df : DivFrame para.diversityII
		local dfHook : DivFrame para.diversityI
		set-width df.width
		include : df.markSet.p
		include : FlatHookDotlessJShape df dfHook XH
			crossLeft -- (df.middle - (dfHook.middle - [mix 0 dfHook.leftSB : mix 1 dfHook.div 2]))
			barCenter -- (df.middle + 0.25 * JBalance * df.div * [mix 1 df.div 2])
			serif -- false

	create-glyph 'dotlessj.flatHookSerifed' : glyph-proc
		local df : DivFrame para.diversityI
		set-width df.width
		include : df.markSet.p
		include : FlatHookDotlessJShape df df XH
			crossLeft -- [mix 0 SB : mix 1 df.div 2]
			barCenter -- (df.middle + JBalance * df.div)
			serif -- true
		include : OverrideJMarks df (JBalance * df.div)

	define [DiagTailDotlessJShape] : params [df dfH barCenter serif] : glyph-proc
		if serif : include : LeftwardTopSerif barCenter XH (LongJut * df.div)
		include : dispiro
			widths.center
			flat barCenter XH [heading Downward]
			DiagonalTailL df barCenter Descender [DiagonalTailStdDepth dfH Stroke] Stroke
		set-base-anchor 'above' barCenter XH
		set-base-anchor 'overlay' barCenter (XH / 2)

	create-glyph 'dotlessj.diagonalTailedSerifless' : glyph-proc
		local df : DivFrame para.diversityII
		local dfHook : DivFrame [mix para.diversityI para.diversityII 0.5]
		set-width df.width
		include : df.markSet.p
		include : DiagTailDotlessJShape df dfHook
			barCenter -- (df.middle + 0.5 * JBalance * df.div * [mix 1 df.div 2])
			serif -- false

	create-glyph 'dotlessj.diagonalTailedSerifed' : glyph-proc
		local df : DivFrame para.diversityI
		set-width df.width
		include : df.markSet.p
		include : DiagTailDotlessJShape df df
			barCenter -- (df.middle + JBalance * df.div)
			serif -- true
		include : OverrideJMarks df (JBalance * df.div)

	select-variant 'dotlessj' 0x237
	link-reduced-variant 'dotlessj/sansSerif' 'dotlessj' MathSansSerif
	CreateDottedComposition 'dotlessj' 'j' 'j'
	CreateDottedComposition 'dotlessj' 'grek/yot' 0x3F3
	CreateDottedComposition 'dotlessj' 'cyrl/je' 0x458
	CreateDottedComposition 'dotlessj/sansSerif' 'j/sansSerif' null
	link-reduced-variant 'j/sansSerif' 'j' MathSansSerif

	create-glyph 'dotlessjCurlyTail.serifless' : glyph-proc
		include : MarkSet.p
		set-base-anchor 'above' (Middle + JBalance) XH
		local fine : AdviceStroke 3.5
		local rinner : LongJut / 2 - fine / 2
		local m1 : Middle - HalfStroke * HVContrast + JBalance
		local x2 : mix RightSB m1 0.25
		local y2 : Descender + O
		include : dispiro
			widths.lhs
			flat m1 XH [heading Downward]
			curl m1 (Descender + fine + rinner * 2)
			CurlyTail fine rinner m1 Descender (m1 - LongJut) x2 y2

	create-glyph 'dotlessjCurlyTail.serifed' : glyph-proc
		include [refer-glyph 'dotlessjCurlyTail.serifless'] AS_BASE ALSO_METRICS
		include : LeftwardTopSerif (Middle + JBalance) XH LongJut

	select-variant 'dotlessjCurlyTail'
	CreateDottedComposition 'dotlessjCurlyTail' 'jCurlyTail' 0x29D
