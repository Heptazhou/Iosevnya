$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"
import [Dotless CvDecompose MathSansSerif] from"../../../support/gr.mjs"

glyph-module

glyph-block Letter-Latin-Lower-B : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateAccentedComposition
	glyph-block-import Letter-Shared-Shapes : DToothlessRise DMBlend HooktopLeftBar
	glyph-block-import Letter-Shared-Shapes : LetterBarOverlay PalatalHook

	define [ToothedBody] : union
		OBarLeftShape
		VBarLeft SB 0 Ascender

	define [ToothlessCornerBody] : union
		OBarLeftToothlessShape (rise -- DToothlessRise) (mBlend -- DMBlend)
		VBarLeft SB DToothlessRise Ascender

	define [ToothlessRoundedBody] : OBarLeftRoundedShape (yTerminal -- Ascender)

	define [FullSerifs] : glyph-proc
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif SB Ascender SideJut
			include : tagged 'serifLB' : LeftwardBottomSerif SB 0 SideJut

	define [LTSerifs] : glyph-proc
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif SB Ascender SideJut

	define [MotionSerifs] : tagged 'serifLT' : LeftwardTopSerif SB Ascender SideJut

	define BConfig : object
		toothed                       { ToothedBody          FullSerifs   SLAB }
		toothlessCorner               { ToothlessCornerBody  LTSerifs     SLAB }
		toothlessRounded              { ToothlessRoundedBody LTSerifs     SLAB }
		toothedMotionSerifed          { ToothedBody          MotionSerifs true }
		toothlessCornerMotionSerifed  { ToothlessCornerBody  MotionSerifs true }
		toothlessRoundedMotionSerifed { ToothlessRoundedBody MotionSerifs true }

	foreach { suffix { Body Serifs doTS }} [Object.entries BConfig] : do
		local yOverlay : mix XH (Ascender - [if doTS Stroke 0]) 0.5
		create-glyph "b.\(suffix)" : glyph-proc
			include : MarkSet.b
			include : Body
			include : Serifs
			set-base-anchor 'overlayOnBar' (SB + 0.5 * Stroke * HVContrast) yOverlay
			set-base-anchor 'overlay' Middle (XH / 2)

		create-glyph "bBar.\(suffix)" : glyph-proc
			include [refer-glyph "b.\(suffix)"] AS_BASE ALSO_METRICS
			include : LetterBarOverlay.l.in SB XH (Ascender - [if doTS Stroke 0])

		create-glyph "bSlash.\(suffix)" : glyph-proc
			include [refer-glyph "b.\(suffix)"] AS_BASE ALSO_METRICS
			include : dispiro
				widths.center OverlayStroke
				flat [mix SB 0       0.7]   (yOverlay - Ascender * 0.1)
				curl [mix SB RightSB 0.625] (yOverlay + Ascender * 0.05)

	select-variant 'b'      'b'
	select-variant 'bBar'   0x180  (follow -- 'b')
	select-variant 'bSlash' 0x2422 (follow -- 'b')

	derive-composites 'bPalatalHook' 0x1D80 'b' : PalatalHook.r
		x -- [mix (Middle + CorrectionOMidS) RightSB 0.75]
		y -- 0
		yAttach -- [mix O SmallArchDepthA : archv.yFromX 0.75]

	link-reduced-variant 'b/sansSerif' 'b' MathSansSerif
	CreateAccentedComposition 'bTildeOver' 0x1D6C 'b' 'tildeOverOnBar'

	create-glyph 'bHookTop.toothed' : glyph-proc
		include : MarkSet.b
		include : OBarLeftShape
		include : HooktopLeftBar.inner
		if SLAB : begin
			include : tagged 'serifLB' : LeftwardBottomSerif SB 0 SideJut

	create-glyph 'bHookTop.toothlessCorner' : glyph-proc
		include : MarkSet.b
		include : OBarLeftToothlessShape (rise -- DToothlessRise) (mBlend -- DMBlend)
		include : HooktopLeftBar.inner (bottom -- DToothlessRise)

	create-glyph 'bHookTop.toothlessRounded' : glyph-proc
		include : MarkSet.b
		include : OBarLeftRoundedShape (yTerminal -- XH)
		include : HooktopLeftBar.inner (bottom -- SmallArchDepthB)

	select-variant 'bHookTop' 0x253

	derive-glyphs 'bdot' 0x1E03 'b' : lambda [src gr] : glyph-proc
		include : refer-glyph "dotAbove"
		include : Translate (Width + HalfStroke) 0
		include [refer-glyph src] AS_BASE

	derive-glyphs 'latinbe' 0x183 'b' : lambda [src gr] : glyph-proc
		include [refer-glyph src] AS_BASE
		include : HBarTop (SB - O) [mix SB RightSB 0.9] Ascender
		if SLAB : begin
			include : DownwardRightSerif [mix SB RightSB 0.9] Ascender VJut

	glyph-block-import Letter-Blackboard : BBS BBD BBBarLeft
	create-glyph 'mathbb/b' 0x1D553 : glyph-proc
		include : MarkSet.b
		include : BBBarLeft SB 0 Ascender
		include : intersection
			OShapeOutline.NoOvershoot XH 0 (SB + BBD + BBS * HVContrast) RightSB
			VBarRight (RightSB - BBD - OX) 0 XH BBS
		include : OBarLeftShape
			left -- SB + BBD
			top  -- XH
			stroke -- BBS
