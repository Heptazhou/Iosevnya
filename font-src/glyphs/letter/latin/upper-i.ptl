$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [Dotless CvDecompose MathSansSerif] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Upper-I : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : SetGrekUpperTonos CreateAccentedComposition

	define [ISeriflessShape df top jut] : glyph-proc
		include : VBar df.middle 0 CAP
		set-base-anchor 'trailing' df.middle 0

	define [ISerifShape df top jut] : glyph-proc
		include : VBar df.middle 0 top
		include : CenterBottomSerif df.middle 0   (jut * df.div)
		include : CenterTopSerif    df.middle top (jut * df.div)
		set-base-anchor 'trailing' (df.middle + jut * df.div) 0

	define UpperIConfig : object
		'straight'     { para.diversityI  ISeriflessShape LongJut      }
		'serifless'    { para.diversityII ISeriflessShape LongJut      }
		'serifed'      { para.diversityI  ISerifShape     LongJut      }
		'shortSerifed' { para.diversityI  ISerifShape     MidJutCenter }

	foreach { suffix { div Body jut } } [Object.entries UpperIConfig] : do
		create-glyph "I.\(suffix)" : glyph-proc
			local df : DivFrame div
			set-width df.width
			include : df.markSet.capital
			include : Body df CAP jut

		create-glyph "grek/Iota.\(suffix)" : glyph-proc
			include [refer-glyph "I.\(suffix)"] AS_BASE ALSO_METRICS
			include : SetGrekUpperTonos 0

	select-variant 'I' 'I'
	link-reduced-variant 'I/sansSerif' 'I' MathSansSerif
	select-variant 'grek/Iota' 0x399 (follow -- 'I')
	link-reduced-variant 'grek/Iota/sansSerif' 'grek/Iota' MathSansSerif (follow -- 'I/sansSerif')
	alias 'cyrl/UkrainianI' 0x406 'I'
	alias 'cyrl/Palochka' 0x4C0 'I'

	create-glyph 'smcpI' 0x26A : glyph-proc
		local df : DivFrame para.diversityI
		include : df.markSet.e
		set-width df.width
		include : ISerifShape df XH MidJutCenter

	glyph-block-import Letter-Blackboard : BBS BBD BBBarCenter
	create-glyph 'mathbb/I' 0x1D540 : glyph-proc
		include : MarkSet.capital
		include : BBBarCenter Middle 0 CAP
		include : HBarTop    (Middle - BBD / 2 - Jut) (Middle + BBD / 2 + Jut) CAP BBS
		include : HBarBottom (Middle - BBD / 2 - Jut) (Middle + BBD / 2 + Jut) 0   BBS

	CreateAccentedComposition 'smcpIBarOver' 0x1D7B 'smcpI' 'barOver'
