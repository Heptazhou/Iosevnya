$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Cyrillic-Che : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : LeaningAnchor
	glyph-block-import Letter-Shared-Shapes : SerifFrame RightwardTailedBar DToothlessRise
	glyph-block-import Letter-Shared-Shapes : CyrDescender BottomExtension

	glyph-block-export BODY
	define BODY : object
		STRAIGHT 0
		TAILED   1

	glyph-block-export SERIFS
	define SERIFS : object
		NONE        0
		ALL         1
		LOWER       2
		TOP         3
		TAILED-I    4
		ALL-BGR     5
		TAILED-BGR  6

	glyph-block-export CyrCheShape
	define [CyrCheShape] : with-params [df top pyBar bodyType slabType [sw df.mvs]] : glyph-proc
		local bar : top * [fallback pyBar 0.5]
		include : match bodyType
			[Just BODY.TAILED] : RightwardTailedBar df.rightSB 0 top (sw -- sw)
			__                 : VBar.r df.rightSB 0 top sw
		include : dispiro
			widths.lhs sw
			flat df.leftSB top [heading Downward]
			curl df.leftSB (bar + ArchDepthB - HalfStroke)
			g4 (df.middle + CorrectionOMidS) (bar - HalfStroke)
			g4 (df.rightSB - 1 / 16) (bar - HalfStroke + DToothlessRise) [heading Rightward]

		local sf : SerifFrame.fromDf df top 0
		include : tagged 'serifLT' : match slabType
			[Just SERIFS.ALL]        sf.lt.full
			[Just SERIFS.TOP]        sf.lt.full
			[Just SERIFS.ALL-BGR]    sf.lt.outer
			[Just SERIFS.TAILED-BGR] sf.lt.outer
			[Just SERIFS.LOWER]      sf.lt.outer
			[Just SERIFS.TAILED-I]   sf.lt.outer
			__                   : glyph-proc
		include : tagged 'SerifRT' : match slabType
			[Just SERIFS.ALL]        sf.rt.full
			[Just SERIFS.TOP]        sf.rt.full
			[Just SERIFS.ALL-BGR]    sf.rt.inner
			[Just SERIFS.TAILED-BGR] sf.rt.inner
			__                   : glyph-proc
		include : tagged 'SerifRB' : match slabType
			[Just SERIFS.ALL]        sf.rb.fullSide
			[Just SERIFS.ALL-BGR]    sf.rb.fullSide
			[Just SERIFS.LOWER]      sf.rb.outer
			__                   : glyph-proc

	create-glyph 'cyrl/Che' 0x427 : glyph-proc
		include : MarkSet.capital
		include : LeaningAnchor.Below.VBar.r RightSB
		include : CyrCheShape [DivFrame 1] CAP [if SLAB 0.45 0.35] BODY.STRAIGHT
			if SLAB SERIFS.ALL SERIFS.NONE

	create-glyph 0x4B6 : composite-proc
		refer-glyph 'cyrl/Che'
		CyrDescender.rSideJut RightSB 0
	create-glyph 0x4CB : composite-proc
		refer-glyph 'cyrl/Che'
		CyrDescender.lSideJut (RightSB - [HSwToV Stroke]) 0 (jut -- MidJutSide)

	glyph-block-export ItalicConfig
	define ItalicConfig : object
		standard { BODY.STRAIGHT SERIFS.ALL SERIFS.ALL-BGR    SERIFS.LOWER    }
		tailed   { BODY.TAILED   SERIFS.TOP SERIFS.TAILED-BGR SERIFS.TAILED-I }

	foreach { suffix { body slabUpright slabUprightBGR slabItalic } } [pairs-of ItalicConfig] : do
		create-glyph "cyrl/che.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : LeaningAnchor.Below.VBar.r RightSB
			include : CyrCheShape [DivFrame 1] XH [if SLAB 0.45 0.4] body
				if SLAB [if para.isItalic slabItalic slabUpright] SERIFS.NONE

		create-glyph "cyrl/che.BGR.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : LeaningAnchor.Below.VBar.r RightSB
			include : CyrCheShape [DivFrame 1] XH [if SLAB 0.45 0.4] body
				if SLAB [if para.isItalic slabItalic slabUprightBGR] SERIFS.NONE

	select-variant 'cyrl/che' 0x447
	select-variant 'cyrl/che.BGR' (follow -- 'cyrl/che')

	create-glyph 0x4B7 : composite-proc
		refer-glyph 'cyrl/che.standard'
		CyrDescender.rSideJut RightSB 0
	create-glyph 0x4CC : composite-proc
		refer-glyph 'cyrl/che.standard'
		CyrDescender.lSideJut (RightSB - [HSwToV Stroke]) 0 (jut -- MidJutSide)

	turned 'cyrl/Shha' 0x4BA 'cyrl/Che' Middle (CAP / 2)
	derive-composites 'cyrl/ShhaDescender' 0x526 'cyrl/Shha' [CyrDescender.rSideJut RightSB 0]

	define [ChrVBar top] : begin
		local SwCheVBar : Math.min OverlayStroke (0.625 * (RightSB - SB - [HSwToV : 2 * Stroke]) / HVContrast)
		local yc : top * [if SLAB 0.45 0.35] + Stroke * 0.1
		return : VBar.m Middle (yc + LongJut * 0.8) (yc - LongJut * 0.8) SwCheVBar

	derive-composites 'cyrl/CheVBar' 0x4B8 'cyrl/Che' [ChrVBar CAP]
	derive-composites 'cyrl/cheVBar' 0x4B9 'cyrl/che' [ChrVBar XH]
