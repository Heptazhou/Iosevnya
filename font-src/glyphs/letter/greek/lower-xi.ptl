$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Lower-Xi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	create-glyph 'grek/xi' 0x3BE : glyph-proc
		include : MarkSet.if
		include : HBarTop SB RightSB CAP

		local smaTop : SmoothAOf (Smooth * 0.45) Width
		local smbTop : SmoothBOf (Smooth * 0.45) Width
		local smaBot : SmoothAOf (Smooth * 0.55) Width
		local smbBot : SmoothBOf (Smooth * 0.55) Width

		local fine : Stroke * CThin
		local xbar : mix SB RightSB 0.85
		local ybar : mix 0 CAP 0.55
		include : dispiro
			g4.left.start xbar (CAP - Stroke + fine) [widths.lhs fine]
			archv 16
			g4 (SB - O * 2) [YSmoothMidL CAP (ybar - Stroke) smaTop smbTop] [widths.lhs]
			arcvh 16
			g4.right.end xbar (ybar - fine) [widths.lhs.heading fine Rightward]
		include : dispiro
			g4.left.start xbar (ybar - Stroke + fine) [widths.lhs.heading fine Leftward]
			archv 16
			g4 SB (0.9 * [YSmoothMidL (ybar - Stroke) Stroke smaBot smbBot]) [widths.lhs]
			arcvh 16
			g4 [mix (SB + HVContrast * Stroke) (RightSB - HVContrast * Stroke) 0.6] 0 [heading Rightward]
			archv 16
			g4.down.mid (RightSB - Stroke * HVContrast) [mix Descender Stroke 0.5] [heading Downward]
			arcvh 16
			flat (RightSB - (Stroke - [mix Descender Stroke 0.5]) * 1.1) (Descender + Stroke)
			curl [Math.min (RightSB - (Stroke - [mix Descender Stroke 0.5]) * 1.1 - 1) [mix SB RightSB 0.5]] (Descender + Stroke)
