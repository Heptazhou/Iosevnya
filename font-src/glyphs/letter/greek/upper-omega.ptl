$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Upper-Omega : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : SetGrekUpperTonos

	define [OmegaShape top extend ada adb] : glyph-proc
		local fine : Stroke * CThin
		local x1 [mix SB RightSB 0.4]
		local x2 (Width - x1)
		local yattach : top * extend + Stroke
		include : dispiro
			g4   x1 (yattach - fine) [widths.heading 0 fine Leftward]
			archv
			flat SB (yattach - Stroke + adb) [widths 0 Stroke]
			curl SB (top - ada)
			arcvh
			g4   (Middle - CorrectionOMidS) (top - O)
			archv
			flat RightSB (top - adb)
			curl RightSB (yattach - Stroke + ada)
			arcvh
			g4   x2 (yattach - fine) [widths.heading 0 fine Leftward]
		include : VBarRight x1 0 (yattach) fine
		include : HBar SB x1 HalfStroke
		include : VBarLeft x2 0 (yattach) fine
		include : HBar x2 RightSB HalfStroke

	create-glyph 'grek/Omega' 0x3A9 : glyph-proc
		include : MarkSet.capital
		include : SetGrekUpperTonos 0
		include : OmegaShape CAP 0.08 ArchDepthA ArchDepthB

	create-glyph 'latinomega' 0xAB65 : glyph-proc
		include : MarkSet.e
		include : OmegaShape XH 0.08 SmallArchDepthA SmallArchDepthB

	turned 'latinUpsilon1' 0x1B1 'grek/Omega' Middle (CAP / 2)
	turned 'latinupsilon1' 0x28A 'latinomega' Middle (XH / 2)
