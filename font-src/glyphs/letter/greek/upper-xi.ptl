$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Upper-Xi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	create-glyph 'grek/Xi' 0x39E : glyph-proc
		include : MarkSet.capital
		local xMidLeft  : mix SB RightSB 0.125
		local xMidRight : mix RightSB SB 0.125
		local yBar : [mix 0 CAP 0.5] + Stroke * 0.125
		include : HBar xMidLeft xMidRight yBar
		include : HBarTop (SB + OX) (RightSB - OX) CAP
		include : HBarBottom (SB + OX) (RightSB - OX) 0
		if SLAB : begin
			include : DownwardLeftSerif (SB + OX) CAP VJut
			include : DownwardRightSerif (RightSB - OX) CAP VJut
			include : UpwardLeftSerif (SB + OX) 0 VJut
			include : UpwardRightSerif (RightSB - OX) 0 VJut
			include : VBarLeft xMidLeft (yBar - 0.5 * VJut) (yBar + 0.5 * VJut) [AdviceStroke 3.5]
			include : VBarRight xMidRight (yBar - 0.5 * VJut) (yBar + 0.5 * VJut) [AdviceStroke 3.5]
