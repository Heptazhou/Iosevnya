### Punctuation symbols
$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'

glyph-module

glyph-block Symbol-Punctuation-Ligation-Variants : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Symbol-Punctuation-Small
	glyph-block-import Symbol-Punctuation-Interpuncts
	glyph-block-import Symbol-Punctuation-Emotion

	define dotLikePunctuations : list
		* "colon"
		* "colon/mid"
		* "semicolon"
		* "period"
		* "period/mid"
		* "comma"
		* "exclam"

	define dotLikeTwoPartPunctuations : list
		* { "question" "question/hookPart" "question/dotPart" }

	define deltas : object
		"dRight"       1.0
		"dRightHalf"   0.5
		"dMid"         0
		"dLeftHalf"  (-0.5)
		"dLeft"      (-1.0)

	define [LigD baseName suffix pDelta] : if [glyph-is-needed baseName] : begin
		local delta : Math.max 0 : Width / 2 - (XH - PeriodSize) / 2
		derive-glyphs (baseName + '.lig.' + suffix) null baseName : function [src sel]
			composite-proc
				refer-glyph src
				Translate (delta * pDelta ) 0

	define [LigDTwoParts baseName aPart bPart suffix pDelta] : if [glyph-is-needed baseName] : begin
		local delta : Math.max 0 : Width / 2 - (XH - PeriodSize) / 2
		derive-glyphs (aPart + '.lig.' + suffix) null aPart : function [src sel] : glyph-proc
			include [refer-glyph src] AS_BASE ALSO_METRICS
			include : Translate (delta * pDelta ) 0
			set-base-anchor 'cvDecompose' 0 0
		derive-glyphs (bPart + '.lig.' + suffix) null bPart : function [src sel] : glyph-proc
			include [refer-glyph src] AS_BASE ALSO_METRICS
			include : Translate (delta * pDelta ) 0
			set-mark-anchor 'cvDecompose' (-Width) 0
		create-two-part-glyph (baseName + '.lig.' + suffix) null (aPart + '.lig.' + suffix) (bPart + '.lig.' + suffix)

	foreach { suffix pDelta } [Object.entries deltas] : begin
		foreach baseName [items-of dotLikePunctuations] : begin
			LigD baseName suffix pDelta
		foreach {baseName aPart bPart} [items-of dotLikeTwoPartPunctuations] : begin
			LigDTwoParts baseName aPart bPart suffix pDelta
