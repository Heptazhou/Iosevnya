### Punctuation symbols
$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'

glyph-module

glyph-block Symbol-Punctuation-Emotion : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Shared-Symbol-Shapes : DotVariants

	local questionBottom : Math.max (CAP * 0.3) (DotSize * 1.5)
	local [questionYConnectT top questionBottom] : mix questionBottom (top - HalfStroke) 0.375

	foreach { suffix { DrawAt kdr overshoot } } [Object.entries DotVariants] : do
		local dr : DotRadius * kdr
		create-glyph "exclam.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityF
			set-width df.width
			include : MarkSet.capital
			include : VBar df.middle [clamp 0 (CAP * 0.4) (questionBottom * 1.18)] CAP
			include : DrawAt df.middle dr (dr - overshoot)

		create-glyph "question/dotPart.\(suffix)" : glyph-proc
			set-width 0
			include : DrawAt (Middle - Width) dr (dr - overshoot)
			set-mark-anchor 'cvDecompose' (0 - Width) 0

		create-glyph "questionDown/dotPart.\(suffix)" : glyph-proc
			set-width 0
			include : DrawAt (Middle - Width) (XH - dr) (dr - overshoot)
			set-mark-anchor 'cvDecompose' (0 - Width) 0

	select-variant 'exclam' '!' (follow -- 'punctuationDot')
	select-variant 'question/dotPart' (follow -- 'punctuationDot')
	select-variant 'questionDown/dotPart' (follow -- 'punctuationDot')

	alias 'alveolarclick' 0x1C3 'exclam'
	turned 'exclamDown'  0xA1   'exclam'  [DivFrame para.diversityF].middle (XH / 2)

	define [QuestionSmoothShape top questionBottom left right] : glyph-proc
		define strokeEndCor : Math.hypot 1 TanSlope
		include : dispiro
			widths.rhs
			g4 left (top - Hook)
			hookstart (top - O)
			g4 right (top - SmoothB * 0.9)
			alsoThru.g2 0.5 0.45 [widths.center EssQuestion]
			straight.down.end ([mix left right 0.5] - HalfStroke * HVContrast * strokeEndCor) questionBottom [widths.heading (Stroke * strokeEndCor) 0 Downward]

	define [QuestionCornerShape top questionBottom left right] : glyph-proc
		local questionYConnect : questionYConnectT top questionBottom
		include : dispiro
			widths.rhs
			g4 left (top - Hook)
			hookstart (top - O)
			g4 (right - OX) [YSmoothMidR top (questionYConnect - Stroke) SmoothA SmoothB]
			arcvh
			straight.left.end [mix left right 0.5] (questionYConnect - Stroke) [heading Leftward]
		include : VBar [mix left right 0.5] questionBottom questionYConnect

	define [QuestionCornerFlatShape top questionBottom left right] : glyph-proc
		local questionYConnect : questionYConnectT top questionBottom
		local depth : Math.max (Stroke * 1.5) ((right - left) * 0.5)
		include : dispiro
			widths.rhs
			flat [mix left right 0.125] top [heading Rightward]
			curl (right - depth - CorrectionOMidS) top
			archv
			g4 (right - OX) [YSmoothMidR top (questionYConnect - Stroke) SmoothA SmoothB]
			arcvh
			straight.left.end [mix left right 0.5] (questionYConnect - Stroke) [heading Leftward]
		include : VBar [mix left right 0.5] questionBottom questionYConnect

	define QuestionConfig : object
		smooth           { QuestionSmoothShape     }
		corner           { QuestionCornerShape     }
		cornerFlatHooked { QuestionCornerFlatShape }

	foreach { suffix { Body } } [Object.entries QuestionConfig] : do
		create-glyph "question/hookPart.\(suffix)" : glyph-proc
			include : Body CAP questionBottom SB RightSB
			set-base-anchor 'cvDecompose' 0 0

		create-glyph "questionDown/hookPart.\(suffix)" : glyph-proc
			include : Body CAP questionBottom SB RightSB
			include : FlipAround Middle (XH / 2)
			set-base-anchor 'cvDecompose' 0 0

		define [InterroBangBodyImpl] : glyph-proc
			local angle : 12 / 180 * Math.PI
			include : Body CAP questionBottom [mix Middle SB 0.75] RightSB
			include : Translate (-Middle) (-DotRadius)
			include : Rotate (-2 * angle)
			include : Translate Middle DotRadius
			include : VBar Middle questionBottom CAP
			include : Translate (-Middle) (-DotRadius)
			include : Rotate (1.2 * angle)
			include : Translate Middle DotRadius

		create-glyph "interrobang/bodyPart.\(suffix)" : glyph-proc
			include : InterroBangBodyImpl
			set-base-anchor 'cvDecompose' 0 0

		create-glyph "interrobangDown/bodyPart.\(suffix)" : glyph-proc
			include : InterroBangBodyImpl
			include : FlipAround Middle (XH / 2)
			set-base-anchor 'cvDecompose' 0 0

	select-variant 'question/hookPart' (follow -- 'question')
	create-two-part-glyph 'question' '?' 'question/hookPart' 'question/dotPart'

	select-variant 'questionDown/hookPart' (follow -- 'question')
	create-two-part-glyph 'questionDown' 0xBF 'questionDown/hookPart' 'questionDown/dotPart'

	select-variant 'interrobang/bodyPart' (follow -- 'question')
	create-two-part-glyph 'interrobang' 0x203D 'interrobang/bodyPart' 'question/dotPart'

	select-variant 'interrobangDown/bodyPart' (follow -- 'question')
	create-two-part-glyph 'gnaborretni'  0x2E18 'interrobangDown/bodyPart' 'questionDown/dotPart'
