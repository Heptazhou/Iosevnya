$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'

glyph-module

glyph-block Symbol-Pictograph-Checking-Marks : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	local light : AdviceStroke 4.5
	local medium : Math.max (light * 1.75) (Width * 0.2)
	local heavy : Math.max (light * 2.5) (Width * 0.3)

	for-width-kinds WideWidth1 : do
		define ptMaxWidth : MosaicWidth - SB
		define ptHeight0 : 1.5 * (Width - SB / 2) * [Math.pow (MosaicWidth / MosaicUnitWidth) (1 / 4)]
		define ptHeight  : ptHeight0 * ptHeight0 / [Math.hypot ptHeight0 : Math.min ptHeight0 ptMaxWidth]
		define ptWidth  : Math.min ptMaxWidth ptHeight
		define ptMidX : MosaicWidth / 2
		define ptSB : (MosaicWidth - ptWidth) / 2
		define ptRSB : MosaicWidth - ptSB
		define ptTop : SymbolMid + ptHeight / 2
		define ptBot : SymbolMid - ptHeight / 2

		foreach [{unicode sw} : items-of {{0x2713 light} {0x2714 medium}}] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : dispiro
					widths.center sw
					g4 ptSB [mix ptBot ptTop 0.46]
					alsoThru 0.5 0.4
					g4 [mix ptSB ptRSB 0.4] [mix ptBot ptTop 0.05] [heading Downward]
				include : dispiro
					widths.center sw
					g4 [mix ptSB ptRSB 0.4] [mix ptBot ptTop 0.05] [heading Upward]
					alsoThru 0.5 0.6
					g4 ptRSB [mix ptBot ptTop 0.9]

		foreach [{unicode sw} : items-of {{0x2715 light} {0x2716 medium}} ] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : dispiro [widths.center sw] [g4 ptRSB ptTop] [g4 ptSB  ptBot]
				include : dispiro [widths.center sw] [g4 ptSB  ptTop] [g4 ptRSB ptBot]

		foreach [{unicode sw} : items-of {{0x2717 light} {0x2718 medium}} ] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : dispiro
					widths.center sw
					g4 [mix ptSB ptRSB 1.05] [mix SymbolMid ptTop 1.02]
					alsoThru 0.53 0.45
					g4 [mix ptSB ptRSB (-0.05)] [mix SymbolMid ptBot 1.05]
				include : dispiro
					widths.center sw
					g4 ptSB [mix ptBot ptTop 0.96]
					alsoThru 0.47 0.5
					g4 ptRSB [mix ptBot ptTop 0.05]

	foreach [{unicode sw op} : items-of {
		{ 0x276C light 1 }
		{ 0x276D light 0 }
		{ 0x276E medium 1 }
		{ 0x276F medium 0 }
		{ 0x2770 heavy 1 }
		{ 0x2771 heavy 0 }
	}] : begin
		create-glyph [NameUni unicode] unicode : glyph-proc
			include : dispiro
				widths.center sw
				corner [mix (SB + sw / 2) (RightSB - sw / 2) op] PictTop [heading Downward]
				corner [mix (SB + sw / 2) (RightSB - sw / 2) (1 - op)] SymbolMid [heading Downward]
				corner [mix (SB + sw / 2) (RightSB - sw / 2) op] PictBot [heading Downward]
