$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'

glyph-module

glyph-block Symbol-Pictograph-Faces : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	for-width-kinds WideWidth1
		define faceTop : mix SymbolMid PictTop 0.75
		define faceBot : mix SymbolMid PictBot 0.75
		define faceWidth : Math.min (MosaicWidth - SB * 2) (faceTop - faceBot)
		define faceMiddle : MosaicWidth / 2
		define faceLeft : faceMiddle - faceWidth / 2
		define faceRight : faceMiddle + faceWidth / 2
		define faceSma : SmoothAOf (SmallSmooth * MosaicWidthScalar) MosaicWidth
		define faceSmb : SmoothBOf (SmallSmooth * MosaicWidthScalar) MosaicWidth

		define faceStroke : AdviceStroke 5 ((faceWidth + SB * 2) / Width)
		define faceInnerStroke : AdviceStroke 8 ((faceWidth + SB * 2) / Width)
		define eyeRadius : DotRadius / Stroke * faceInnerStroke
		define eyeHeight    0.625
		define mouthDepth : 0.025 * [Math.sqrt MosaicWidthScalar]

		define [FaceRing F] : F faceTop faceBot faceLeft faceRight faceStroke faceSma faceSmb
		define [SmileFace k width] : union
			DotAt (faceMiddle - width / 2 + eyeRadius / 2) [mix faceBot faceTop eyeHeight] eyeRadius
			DotAt (faceMiddle + width / 2 - eyeRadius / 2) [mix faceBot faceTop eyeHeight] eyeRadius
			dispiro
				widths.center faceInnerStroke
				g4 (faceMiddle - width / 2) [mix faceBot faceTop (1 - eyeHeight + k * mouthDepth)]
				g4 (faceMiddle)             [mix faceBot faceTop (1 - eyeHeight - k * mouthDepth)]
				g4 (faceMiddle + width / 2) [mix faceBot faceTop (1 - eyeHeight + k * mouthDepth)]

		create-glyph [MangleName 'whiteSadFace'] [MangleUnicode 0x2639] : glyph-proc
			set-width MosaicWidth
			include : union
				FaceRing OShape
				SmileFace (-1) : 0.6 * (faceWidth - faceStroke * 2)

		create-glyph [MangleName 'whiteSmileFace'] [MangleUnicode 0x263a] : glyph-proc
			set-width MosaicWidth
			include : union
				FaceRing OShape
				SmileFace 1 : 0.6 * (faceWidth - faceStroke * 2)

		create-glyph [MangleName 'blackSmileFace'] [MangleUnicode 0x263B] : glyph-proc
			set-width MosaicWidth
			include : difference
				FaceRing OShapeOutline
				SmileFace 1 : 0.6 * (faceWidth - faceStroke * 2)
