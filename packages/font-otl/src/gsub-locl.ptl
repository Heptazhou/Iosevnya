$$include './meta/macros.ptl'

import [CvDecompose] from "@iosevka/glyph/relation"

extern Set

export : define [buildLOCL gsub para glyphStore] : begin
	local rec : gsub.beginBlock

	define { chain-rule } : gsub.ChainRuleBuilder

	define cyrlSRB  : gsub.copyLanguage 'cyrl_SRB ' 'cyrl_DFLT'
	define cyrlMKD  : gsub.copyLanguage 'cyrl_MKD ' 'cyrl_DFLT'
	define cyrlBGR  : gsub.copyLanguage 'cyrl_BGR ' 'cyrl_DFLT'
	define latnVIT  : gsub.copyLanguage 'latn_VIT ' 'latn_DFLT'

	# SRB
	define loclSRB : gsub.createFeature 'locl'
	cyrlSRB.addFeature loclSRB
	cyrlMKD.addFeature loclSRB
	loclSRB.addLookup : gsub.createLookup
		.type 'gsub_single'
		.substitutions : if para.isItalic
			object
				'cyrl/be'  : glyphStore.ensureExists 'cyrl/be.SRB'
				'cyrl/ghe' : glyphStore.ensureExists 'cyrl/ghe.SRB'
				'cyrl/de'  : glyphStore.ensureExists 'cyrl/de.SRB'
				'cyrl/gje' : glyphStore.ensureExists 'cyrl/gje.SRB'
				'cyrl/pe'  : glyphStore.ensureExists 'cyrl/pe.SRB'
				'cyrl/te'  : glyphStore.ensureExists 'cyrl/te.SRB'
			object
				'cyrl/be'  : glyphStore.ensureExists 'cyrl/be.SRB'

	# BGR
	define loclBGR : cyrlBGR.addFeature : gsub.createFeature 'locl'
	loclBGR.addLookup : gsub.createLookup
		.type 'gsub_single'
		.substitutions : object
			'cyrl/ve'      : glyphStore.ensureExists 'cyrl/ve.BGR'
			'cyrl/ghe'     : glyphStore.ensureExists 'cyrl/ghe.italic'
			'cyrl/De'      : glyphStore.ensureExists 'cyrl/De.BGR'
			'cyrl/de'      : glyphStore.ensureExists 'cyrl/de.BGR'
			'cyrl/zhe'     : glyphStore.ensureExists 'cyrl/zhe.BGR'
			'cyrl/ze'      : glyphStore.ensureExists 'cyrl/ze.BGR'
			'cyrl/i'       : glyphStore.ensureExists 'cyrl/i.BGR'
			'cyrl/ibreve'  : glyphStore.ensureExists 'cyrl/ibreve.BGR'
			'cyrl/igrave'  : glyphStore.ensureExists 'cyrl/igrave.BGR'
			'cyrl/ka'      : glyphStore.ensureExists 'cyrl/ka.BGR'
			'cyrl/El'      : glyphStore.ensureExists 'cyrl/El.BGR'
			'cyrl/el'      : glyphStore.ensureExists 'cyrl/el.BGR'
			'cyrl/en'      : glyphStore.ensureExists 'cyrl/en.BGR'
			'cyrl/pe'      : glyphStore.ensureExists 'cyrl/pe.BGR'
			'cyrl/te'      : glyphStore.ensureExists 'cyrl/te.BGR'
			'cyrl/Ef'      : glyphStore.ensureExists 'cyrl/Ef.BGR'
			'cyrl/ef'      : glyphStore.ensureExists 'cyrl/ef.BGR'
			'cyrl/che'     : glyphStore.ensureExists 'cyrl/che.BGR'
			'cyrl/sha'     : glyphStore.ensureExists 'cyrl/sha.BGR'
			'cyrl/shcha'   : glyphStore.ensureExists 'cyrl/shcha.BGR'
			'cyrl/yu'      : glyphStore.ensureExists 'cyrl/yu.BGR'
			'cyrl/tse'     : glyphStore.ensureExists 'cyrl/tse.BGR'
			'cyrl/yer'     : glyphStore.ensureExists 'cyrl/yer.BGR'
			'cyrl/yeri'    : glyphStore.ensureExists 'cyrl/yeri.BGR'

	# VIT
	define loclVIT : latnVIT.addFeature : gsub.createFeature 'locl'

	define [sx s] : lambda [t] : t.map : lambda [x] "\(x)/\(s)"

	do "Decompose Vietnamese glyphs"

		local decompositionSubstitutions {.}
		loclVIT.addLookup : gsub.createLookup
			.type 'gsub_multiple'
			.substitutions decompositionSubstitutions

		local viCircumflex 'ẦẤẨỀẾỂỒỐỔầấẩềếểồốổ'
		local viBreve      'ẮẰẲằắẳ'
		local viShiftableMarks { 'acuteAbove' 'graveAbove' 'hookAbove' }

		foreach ch (viCircumflex + viBreve) : begin
			local u : ch.codePointAt 0
			local gn : glyphStore.queryNameByUnicode u
			local g : glyphStore.queryByNameEnsured gn
			local decomp : CvDecompose.get g
			set decompositionSubstitutions.(gn) decomp

		loclVIT.addLookup : gsub.createLookup
			.type 'gsub_chaining'
			.rules : list
				chain-rule
					{'A' 'a' 'E' 'e' 'O' 'o'} ~> nothing
					{'circumflexAbove'}       ~> nothing
					viShiftableMarks          ~> [sx 'viSide']
				chain-rule
					{'A' 'a'}                 ~> nothing
					{'breveAbove'}            ~> nothing
					viShiftableMarks          ~> [sx 'viCenter']

	gsub.endBlockAtFront rec
